buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

// 所有子项目的通用配置
allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'com.olivia'
    version '1.0'

    // JVM 版本号要求
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // java编译的时候缺省状态下会因为中文字符而失败
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    //定义版本号
    ext {

    }
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        maven {
            url "http://mvnrepository.com/artifact/"
        }
    }

    // 移除依赖
    configurations {
        all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        all*.exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
        all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
        all*.exclude group: 'log4j', module: 'log4j'
    }

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('io.dropwizard.metrics:metrics-core') with module('io.dropwizard.metrics:metrics-core:4.0.2')
            }
        }
    }

    dependencies {
        compile(
                "org.springframework.boot:spring-boot-starter-web:$springBootVersion",
                "org.springframework.boot:spring-boot-starter-aop:$springBootVersion",
                "mysql:mysql-connector-java:6.0.6",
                "org.apache.commons:commons-lang3:3.6",
                "org.projectlombok:lombok:1.16.18",
                "com.google.guava:guava:16.0.1",
                //"com.baomidou:mybatis-plus-boot-starter:3.1.1"
        )

        testCompile(
                "junit:junit:4.12",
                "org.springframework.boot:spring-boot-starter-test:$springBootVersion",
                "org.apache.velocity:velocity:1.7")
    }
}